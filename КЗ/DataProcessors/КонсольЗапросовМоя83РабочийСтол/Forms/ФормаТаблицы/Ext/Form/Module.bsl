
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЭтоАдресВременногоХранилища(Параметры.ТЗ_Хранилище) тогда
		ТЗ = ПолучитьИзВременногоХранилища(Параметры.ТЗ_Хранилище);
	Иначе
		ТЗ = Новый ТаблицаЗначений;
		Сообщить("Тз не передалась");
	КонецЕсли;
	СгененироватьТаблицуЗначенийДляФормы("ТЗ", ТЗ);
	ВывестиТЗНаФорму("ТЗ");
	ЗначениеВРеквизитФормы(ТЗ, "ТЗ");
	
КонецПроцедуры

Процедура СгененироватьТаблицуЗначенийДляФормы(ИмяРеквизитаТЗ, ТЗ) Экспорт
	
	МассивТипаТЗ = Новый Массив;
	МассивТипаТЗ.Добавить(Тип("ТаблицаЗначений"));
	ОписаниеТипаТЗ = Новый ОписаниеТипов(МассивТипаТЗ);
	МассивРеквизитов = Новый Массив; 
	
	МассивРеквизитов.Добавить(Новый РеквизитФормы(ИмяРеквизитаТЗ, ОписаниеТипаТЗ, "", "ТЗН"));
	
	Для Каждого Колонка Из ТЗ.Колонки Цикл
		
		МассивРеквизитов.Добавить(Новый РеквизитФормы(Колонка.Имя, Колонка.ТипЗначения, ИмяРеквизитаТЗ));
		
	КонецЦикла;  
	УдаляемыеРеквизиты = Новый Массив;
	СуществующиеРеквизиты = ПолучитьРеквизиты();
	Для каждого СущьРек из СуществующиеРеквизиты цикл
		Если СущьРек.Имя = ИмяРеквизитаТЗ тогда
			УдаляемыеРеквизиты.Добавить(ИмяРеквизитаТЗ);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ИзменитьРеквизиты(МассивРеквизитов,УдаляемыеРеквизиты);
	
КонецПроцедуры 

Процедура ВывестиТЗНаФорму(ИмяТЗ, РодЭлем = Неопределено)
	
	РодЭлем = ?(РодЭлем = Неопределено, ЭтаФорма, РодЭлем);
	ПрошлыйЭлем = ЭтаФорма.Элементы.Найти(ИмяТЗ);
	
	Если ПрошлыйЭлем<>Неопределено тогда
		ЭтаФорма.Элементы.Удалить(ПрошлыйЭлем);
	КонецЕсли;
	
	ТаблицаПолейВыбора = Элементы.Добавить(ИмяТЗ, Тип("ТаблицаФормы"),РодЭлем);
	ТаблицаПолейВыбора.ПутьКДанным = ИмяТЗ;
	ТаблицаПолейВыбора.Отображение = ОтображениеТаблицы.Список; 
	
	ТЗ_Об = РеквизитФормыВЗначение(ИмяТЗ);
	Для Каждого Колонка Из ТЗ_Об.Колонки Цикл
		
		НовыйЭлемент = Элементы.Добавить(Колонка.Имя, Тип("ПолеФормы"), ТаблицаПолейВыбора);       
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.ПутьКДанным = ИмяТЗ + "." + Колонка.Имя;
		НовыйЭлемент.Ширина = 10;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ВыбратьНаСервере()
	
	ТЗ_Об = РеквизитФормыВЗначение("ТЗ");	
	АдресТЗ = ПоместитьВоВременноеХранилище(ТЗ_Об, Параметры.ИдФормы);  
	Возврат АдресТЗ;
	
КонецФункции


&НаКлиенте
Процедура Выбрать(Команда)
	
	АдресТЗ = ВыбратьНаСервере();
	ЭтаФорма.ОповеститьОВыборе(Новый Структура("АдресТЗ,ИдСтроки",АдресТЗ,Параметры.ИдСтроки));
	
КонецПроцедуры


&НаСервере
Процедура ДобавитьКолонкуНаСервере()
	
	Если ПустаяСтрока(рфИмяКолонки) тогда
		Сообщить("Не заполнено имя колонки.");
		Возврат;
	КонецЕсли;
	
	Если рфТипКолонки.Типы().Количество()=0 тогда
		Сообщить("Не выбран тип колонки.");
		Возврат;
	КонецЕсли;
	
	ТЗ_ОБ = РеквизитФормыВЗначение("ТЗ");
	Колонка = ТЗ_Об.Колонки.Добавить(рфИмяКолонки, рфТипКолонки); 
	
	СгененироватьТаблицуЗначенийДляФормы("ТЗ", ТЗ_ОБ);
	ВывестиТЗНаФорму("ТЗ");
	
КонецПроцедуры


&НаКлиенте
Процедура ДобавитьКолонку(Команда)
	ДобавитьКолонкуНаСервере();
КонецПроцедуры

